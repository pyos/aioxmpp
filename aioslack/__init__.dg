import '/json'
import '/time'
import '/asyncio'
import '/aiohttp'
import '/logging'
import '/websockets'
import '/urllib/parse' qualified

import '../events'
import 'struct'
import 'struct/IM'   # These are useful to outside code,
import 'struct/Bot'  # so let's make access more convenient.
import 'struct/User'
import 'struct/Event'
import 'struct/Channel'


Slack = subclass events.EventSystem where
  __init__ = initial token socket ~>
    @log = logging.getLogger 'slack.client'
    @_pool   = aiohttp.TCPConnector!
    @count   = 1
    @token   = token
    @socket  = socket
    @objects = dict!
    @objects !! (initial !! 'team' !! 'id') = @team = struct.Team self $ initial !! 'team'
    for ob in initial !! 'bots'     => @objects !! (ob !! 'id') = Bot     self ob
    for ob in initial !! 'users'    => @objects !! (ob !! 'id') = User    self ob
    for ob in initial !! 'ims'      => @objects !! (ob !! 'id') = IM      self ob
    for ob in initial !! 'channels' => @objects !! (ob !! 'id') = Channel self ob
    @self = @objects !! (initial !! 'self' !! 'id')
    events.EventSystem.__init__ self @socket._loop

  #: Find an object by type and name.
  #:
  #: find :: type str -> Maybe Object
  #:
  find = t name ~> next (filter (x -> x :: t and x.get 'name' == name) @objects.values!) None

  #: Read the next event.
  #:
  #: yield from :: Slack -> Event
  #:
  __iter__ = self ->
    msg = yield from @socket.recv!
    if msg is None => raise ConnectionError
       msg :: str  => Event self $ json.loads msg
       otherwise   => Event self $ json.loads $ msg.decode 'utf-8'

  #: Dispatch all nodes through the event system as `event` events.
  #:
  #: dispatch :: coroutine a
  #:
  dispatch = ~>
    while True =>
      ev = yield from self

      if ev :: Event.Message.Join    => ev.channel.members.append ev.user
         ev :: Event.Message.Leave   => ev.channel.members.remove ev.user
         ev :: Event.Message.Topic   => ev.channel.topic.value   = ev.topic
         ev :: Event.Message.Purpose => ev.channel.purpose.value = ev.purpose
         ev :: Event.Message.Rename  => ev.channel.name = ev.name
         ev :: Event.UserPresence    => ev.user.presence = ev.presence
         ev :: Event.BotNew          => @objects !! ev.bot.id = ev.bot
         ev :: Event.ChannelNew      => @objects !! ev.channel.id = ev.channel
         ev :: Event.UserNew         => @objects !! ev.user.id = ev.user
         ev :: Event.UserChange =>
           old = @objects.get ev.user.id ev.user
           old.update ev.user
           ev.user = old

      if 'reply_to' in ev => @async $ @emit ev.reply_to ev
         otherwise        => @async $ @emit 'event' ev

  #: JSON-encode stuff, then send as text. `yield from` to flush.
  #:
  #: rtm :: object -> coroutine (dict str object)
  #:
  rtm = **: args ~>
    args !! 'id' = eid = @count += 1
    yield from $ @socket.send $ json.dumps args
    yield from $ @wait_for eid

  #: Send a Web API request.
  #:
  #: rpc :: str ** -> coroutine (dict str object)
  #:
  rpc = method **: args ~>
    args !! 'token' = @token
    url  = 'https://slack.com/api/' + method
    data = yield from $ aiohttp.request 'POST' url loop: @loop connector: @_pool data: args
    if data.status == 200 => yield from $ data.json!
       otherwise          => dict ok: False error: (dict code: data.status)

  #: Send a ping, wait for a pong. Requires `dispatch` to be running.
  #:
  #: ping :: coroutine a
  #:
  ping = ~> @rtm type: 'ping'

  #: Send a message to a channel. Requires `dispatch` to be running.
  #:
  #: respond :: Channel str -> coroutine a
  #:
  respond = channel text **: args ~> if
    not args  => yield from $ @rtm type: 'message' channel: channel.id text: text
    otherwise => yield from $ @rpc 'chat.postMessage' channel: channel.id text: text **: args

  #: Update the topic of a channel.
  #:
  #: set_topic :: Channel str -> coroutine a
  #:
  set_topic = channel topic ~>
    yield from $ @rpc 'channels.setTopic' topic: topic channel: channel.id

  #: Start an RTM Slack session.
  #:
  #: start :: str -> Slack
  #:
  start = classmethod $ cls token loop: None ->
    url  = 'https://slack.com/api/rtm.start?token=' + token
    data = yield from $ aiohttp.request 'GET' url loop: loop
    data = yield from $ data.json!
    data !! 'ok' is False => raise $ ConnectionError $ data.get 'error' '?'

    conn = cls data token $ yield from $ websockets.connect loop: loop $ data !! 'url'
    node = yield from conn  # must be `{"type": "hello"}`
    node.error => raise $ ConnectionError $ node.error.msg
    conn


start = Slack.start
